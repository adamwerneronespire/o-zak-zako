FUNCTION /ZAK/GEN_AFA_SZLA.
*"----------------------------------------------------------------------
*"*"Lokális interfész:
*"  TABLES
*"      T_ANALITIKA STRUCTURE  /ZAK/ANALITIKA OPTIONAL
*"      T_AFA_SZLA STRUCTURE  /ZAK/AFA_SZLA OPTIONAL
*"      T_RETURN STRUCTURE  BAPIRET2 OPTIONAL
*"----------------------------------------------------------------------

  DATA L_INDEX TYPE I.
  DATA LW_/ZAK/AFA_SZLA TYPE /ZAK/AFA_SZLA.
  DATA LW_RETURN TYPE BAPIRET2.
  RANGES LS_NYLAPAZON FOR /ZAK/ANALITIKA-NYLAPAZON.
  DATA L_TEXT(40).

  DEFINE LM_SET_MESSAGE.
    CLEAR LW_RETURN.
    LW_RETURN-TYPE   = &2.
    LW_RETURN-ID     = &1.
    LW_RETURN-NUMBER = &3.
    MESSAGE ID &1 TYPE &2 NUMBER &3 WITH &4 &5 &6 &7
            INTO LW_RETURN-MESSAGE.
    LW_RETURN-MESSAGE_V1 = &4.
    LW_RETURN-MESSAGE_V2 = &5.
    LW_RETURN-MESSAGE_V3 = &6.
    LW_RETURN-MESSAGE_V4 = &7.
    APPEND LW_RETURN TO T_RETURN.
  END-OF-DEFINITION.


  LOOP AT T_ANALITIKA INTO W_/ZAK/ANALITIKA WHERE ABEVAZ EQ C_ABEVAZ_DUMMY_R.
    CLEAR W_/ZAK/AFA_SZLA.
    ADD 1 TO L_INDEX.
    MOVE-CORRESPONDING W_/ZAK/ANALITIKA TO W_/ZAK/AFA_SZLA.
    W_/ZAK/AFA_SZLA-NYLAPAZON = W_/ZAK/ANALITIKA-NYLAPAZON(3).
    IF W_/ZAK/AFA_SZLA-BSEG_BELNR IS INITIAL.
      W_/ZAK/AFA_SZLA-BSEG_BELNR = L_INDEX.
    ENDIF.
    APPEND W_/ZAK/AFA_SZLA TO T_AFA_SZLA.
  ENDLOOP.

  SORT T_AFA_SZLA.
* Eredeti sorok meghatározása
  LOOP AT T_AFA_SZLA INTO W_/ZAK/AFA_SZLA WHERE  SZAMLASZA IS INITIAL
                                           AND  SZAMLASZE IS INITIAL.
    W_/ZAK/AFA_SZLA-SZAMLASZA = W_/ZAK/AFA_SZLA-SZAMLASZ.
    W_/ZAK/AFA_SZLA-SZLATIP   = C_SZLATIP_E.
    MODIFY T_AFA_SZLA FROM W_/ZAK/AFA_SZLA.
  ENDLOOP.

* Korrekciós sorok meghatározása (végrehajtjuk kétszer, hogy ha a módosítások
* nem sorrendben következnek akkor is megtaláljuk)
  DO 2 TIMES.
    LOOP AT T_AFA_SZLA INTO W_/ZAK/AFA_SZLA WHERE      SZAMLASZA IS INITIAL
                                             AND  NOT SZAMLASZE IS INITIAL.
      LOOP AT T_AFA_SZLA INTO LW_/ZAK/AFA_SZLA
                        WHERE     BUKRS     = W_/ZAK/AFA_SZLA-BUKRS
                          AND     ADOAZON   = W_/ZAK/AFA_SZLA-ADOAZON
                          AND     NOT SZAMLASZA IS INITIAL
                          AND     SZAMLASZ  = W_/ZAK/AFA_SZLA-SZAMLASZE
                          AND     NYLAPAZON(3) = W_/ZAK/AFA_SZLA-NYLAPAZON(3).
        EXIT.
      ENDLOOP.
      IF SY-SUBRC EQ 0.
        W_/ZAK/AFA_SZLA-SZAMLASZA = LW_/ZAK/AFA_SZLA-SZAMLASZA.
        MODIFY T_AFA_SZLA FROM W_/ZAK/AFA_SZLA  TRANSPORTING SZAMLASZA.
      ENDIF.
    ENDLOOP.
  ENDDO.

* Megpróbáljuk az üreseket keresni az adatbázisban is
  LOOP AT T_AFA_SZLA INTO W_/ZAK/AFA_SZLA WHERE      SZAMLASZA IS INITIAL
                                           AND  NOT SZAMLASZE IS INITIAL.
*   Keressük a 4 hosszú NYLAPAZON-t is
    CONCATENATE W_/ZAK/AFA_SZLA-NYLAPAZON(3) '*' INTO L_TEXT.
    M_DEF LS_NYLAPAZON 'I' 'CP' L_TEXT SPACE.

*++S4HANA#01.
*    SELECT SINGLE SZAMLASZA INTO W_/ZAK/AFA_SZLA-SZAMLASZA
*                            FROM /ZAK/AFA_SZLA
    SELECT SZAMLASZA INTO W_/ZAK/AFA_SZLA-SZAMLASZA
                      FROM /ZAK/AFA_SZLA UP TO 1 ROWS
*--S4HANA#01.
                      WHERE BUKRS     = W_/ZAK/AFA_SZLA-BUKRS
                        AND ADOAZON   = W_/ZAK/AFA_SZLA-ADOAZON
                        AND SZAMLASZA <> ''
                        AND SZAMLASZ = W_/ZAK/AFA_SZLA-SZAMLASZE
                        AND NYLAPAZON IN LS_NYLAPAZON
*++S4HANA#01.
                      ORDER BY PRIMARY KEY.
    ENDSELECT.
*--S4HANA#01.
    IF SY-SUBRC EQ 0.
      MODIFY T_AFA_SZLA FROM W_/ZAK/AFA_SZLA  TRANSPORTING SZAMLASZA.
    ENDIF.
  ENDLOOP.

* Ha még mindig marad üres az hiba:
  LOOP AT T_AFA_SZLA INTO W_/ZAK/AFA_SZLA WHERE    SZAMLASZA IS INITIAL
                                         AND  NOT SZAMLASZE IS INITIAL.
    LM_SET_MESSAGE '/ZAK/ZAK' 'E' '353' W_/ZAK/AFA_SZLA-SZAMLASZE SPACE SPACE SPACE.
  ENDLOOP.

*++1365 24.
  LOOP AT T_ANALITIKA INTO W_/ZAK/ANALITIKA WHERE ABEVAZ EQ C_ABEVAZ_DUMMY_R.
    IF W_/ZAK/ANALITIKA-SZLATIP EQ C_SZLATIP_E.
      W_/ZAK/ANALITIKA-SZAMLASZA = W_/ZAK/ANALITIKA-SZAMLASZ.
    ELSEIF W_/ZAK/ANALITIKA-SZLATIP EQ C_SZLATIP_K.
      READ TABLE T_AFA_SZLA INTO W_/ZAK/AFA_SZLA WITH KEY
                            BUKRS     = W_/ZAK/ANALITIKA-BUKRS
                            ADOAZON   = W_/ZAK/ANALITIKA-ADOAZON
                            SZAMLASZ  = W_/ZAK/ANALITIKA-SZAMLASZ
                            SZAMLASZE = W_/ZAK/ANALITIKA-SZAMLASZE.
      IF SY-SUBRC EQ 0.
        W_/ZAK/ANALITIKA-SZAMLASZA = W_/ZAK/AFA_SZLA-SZAMLASZA.
      ENDIF.
    ENDIF.
    MODIFY T_ANALITIKA FROM W_/ZAK/ANALITIKA TRANSPORTING SZAMLASZA.
  ENDLOOP.
*--1365 24.

ENDFUNCTION.
